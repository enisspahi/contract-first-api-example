plugins {
    id 'java'
    id 'org.springframework.boot' version '3.1.2'
    id 'io.spring.dependency-management' version '1.1.2'
    id "org.openapi.generator"
    id 'io.freefair.lombok' version '8.4'
}

group = 'com.enisspahi.example'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

compileJava.dependsOn tasks.openApiGenerate

sourceSets {
    main {
        java {
            srcDir "${buildDir}/generated/src/main/java"
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'jakarta.validation:jakarta.validation-api:3.0.2'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'au.com.dius.pact.provider:junit5spring:4.6.3'
}

test {
    useJUnitPlatform()
}

springBoot {
    mainClass = "com.enisspahi.example.ContractFirstApiApplication"
}

openApiGenerate {
    generatorName = "spring"
    inputSpec = "$rootDir/api/src/main/resources/recipes-api.yaml"
    outputDir = "$buildDir/generated"
    apiPackage = "com.enisspahi.example.api.controller"
    invokerPackage = "com.enisspahi.example.api"
    modelPackage = "com.enisspahi.example.api.model"
    configOptions = [
            library: "spring-boot",
            dateLibrary: "java8",
            delegatePattern: "true",
            useBeanValidation: "false",
            useOptional: "true",
            openApiNullable: "false",
            annotationLibrary: "none",
            documentationProvider: "none",
            useSwaggerUI: "false",
            useSpringBoot3: "true"
    ]
}

openApiValidate {
    inputSpec.set("$rootDir/api/src/main/resources/recipes-api.yaml")
}