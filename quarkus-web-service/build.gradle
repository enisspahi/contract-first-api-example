plugins {
    id 'java'
    id 'io.quarkus'
    id "org.openapi.generator"
}

group = 'com.enisspahi.example'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    implementation enforcedPlatform("${quarkusPlatformGroupId}:${quarkusPlatformArtifactId}:${quarkusPlatformVersion}")
    implementation 'io.quarkus:quarkus-resteasy-reactive'
    implementation 'io.quarkus:quarkus-resteasy-reactive-jackson'
    implementation 'io.quarkus:quarkus-arc'
    implementation 'org.yaml:snakeyaml:1.8'
    testImplementation 'io.quarkus:quarkus-junit5'
    testImplementation 'io.rest-assured:rest-assured'
}


test {
    systemProperty "java.util.logging.manager", "org.jboss.logmanager.LogManager"
}
compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-parameters'
    dependsOn tasks.openApiGenerate
}

compileTestJava {
    options.encoding = 'UTF-8'
}

sourceSets {
    main {
        java {
            srcDir "${buildDir}/generated/src/gen/java"
        }
    }
}

openApiGenerate {
    generatorName.set("jaxrs-spec")
    inputSpec.set("$rootDir/api/src/main/resources/recipes-api.yaml")
    outputDir.set("$buildDir/generated")
    apiPackage.set("com.enisspahi.example.api.controller")
    invokerPackage.set("com.enisspahi.example.api")
    modelPackage.set("com.enisspahi.example.api.model")
    configOptions.set([
            generateSupportingFiles: "true",
            library: "quarkus",
            dateLibrary: "java8",
            useBeanValidation: "false",
            openApiNullable: "false",
            useSwaggerAnnotations: "false",
            useJakartaEe: "true",
            interfaceOnly: "true",
            reactive: "true"
    ])
}

openApiValidate {
    inputSpec.set("$rootDir/api/src/main/resources/recipes-api.yaml")
}