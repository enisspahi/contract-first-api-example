plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.3'
    id 'io.spring.dependency-management' version '1.1.6'
    id "org.openapi.generator" version "7.8.0"
}

group = 'com.enisspahi.example'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_21
}

compileJava.dependsOn tasks.openApiGenerate

sourceSets {
    main {
        java {
            srcDir "${buildDir}/generated/src/main/java"
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation platform('org.junit:junit-bom:5.9.1')
}

test {
    useJUnitPlatform()
}

openApiGenerate {
    generatorName = "java"
    inputSpec = "$rootDir/api/src/main/resources/recipes-api.yaml"
    outputDir = "$buildDir/generated"
    apiPackage = "com.enisspahi.example.api"
    invokerPackage = "com.enisspahi.example.api.invoker"
    modelPackage = "com.enisspahi.example.api.model"
    apiNameSuffix = "."
    configOptions = [
            library: "native",
            dateLibrary: "java8",
            useJakartaEe: "true",
            openApiNullable: "false",
    ]
}
